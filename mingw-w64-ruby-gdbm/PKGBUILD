# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=gdbm
pkgbase=mingw-w64-ruby-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-ruby-${_realname}")
pkgver=1.10
pkgrel=1
pkgdesc="GNU database library (mingw-w64)"
url="http://www.gnu.org/software/gdbm/"
license=('GPL3')
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-gettext"
         "${MINGW_PACKAGE_PREFIX}-libiconv" )
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('staticlibs')
source=(ftp://ftp.gnu.org/gnu/gdbm/${_realname}-${pkgver}.tar.gz{,.sig}
        'gdbm-1.10-zeroheaders.patch'
        'gdbm-win32-support.patch')
sha256sums=('23f8134c5b94bbfb06d756a6b78f074fba6e6028cf2fe01341d40b26db773441'
            'd85ff239579ee6812c0c7c11e08dba4f79fa9b629ddca3fcb5016d227f82c095'
            '82cacfdc6437520db382e54ee9a637bae843059b72def6efa1a91552f7c26715'
            '0d5219903b5dcf7c081ff98e18ae5e3e3514597be57266454f2e3950c95db41b')

prepare() {
  cd ${srcdir}/${_realname}-${pkgver}

  patch -p1 -i ${srcdir}/gdbm-1.10-zeroheaders.patch
  patch -p1 -i ${srcdir}/gdbm-win32-support.patch

  autoreconf --install --force
}

build() {
  [[ -d ${srcdir}/build-${MINGW_CHOST} ]] && rm -rf ${srcdir}/build-${MINGW_CHOST}
  mkdir ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  ${srcdir}/${_realname}-${pkgver}/configure \
    --build=${MINGW_CHOST} \
    --host=${MINGW_CHOST} \
    --target=${MINGW_CHOST} \
    --prefix=${MINGW_PREFIX} \
    --enable-static \
    --enable-shared \
    --enable-libgdbm-compat

  make -j1
}

check() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -ij1 check
}

package() {
  cd ${srcdir}/build-${MINGW_CHOST}
  make -j1 DESTDIR=${pkgdir} install
}