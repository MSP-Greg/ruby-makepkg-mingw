# Maintainer: Alexey Pavlov <Alexpux@gmail.com>

_realname=openssl
pkgbase=mingw-w64-${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-${_realname}")
_ver=1.1.1-pre10
# use a pacman compatible version scheme
#pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgver=1.1.1_pre10
pkgrel=1
pkgdesc="The Open Source toolkit for Secure Sockets Layer and Transport Layer Security (mingw-w64)"
url="https://www.openssl.org"
license=('BSD')
arch=('any')
depends=("${MINGW_PACKAGE_PREFIX}-ca-certificates"
         "${MINGW_PACKAGE_PREFIX}-gcc-libs"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc")
options=('strip' '!buildflags' 'staticlibs')
# noextract=(${_realname}-${_ver}.tar.gz)

source=('openssl-1.1.1-unix-Makefile.tmpl.patch'
        'openssl-1.1.1-relocation.patch'
        'openssl_master_date.patch')

sha256sums=('b5cce65680a061a0f127c195507a64f0bac37a5b93367018a9b19d27fec64a8f'
            '180372a9ef944211323b8de45824f3a9c2d0bf373e6e0bae6ed7de992355a5fc'
            '2757fb02c7bdb75e430e024f17466b7e272c8c8a2655f7ee8b07a4a96a3683d4')

validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491')

prepare() {
  # Clean up old sources so re-patching doesn't fail.
#  [[ -d ${srcdir}/${_realname}-${_ver} ]] && rm -rf ${srcdir}/${_realname}-${_ver}
#  tar -xzvf ${_realname}-${_ver}.tar.gz -C ${srcdir} || true
  cd ${srcdir}/${_realname}-${_ver}

  patch -p1 -i ${srcdir}/openssl-1.1.1-unix-Makefile.tmpl.patch
  patch -p1 -i ${srcdir}/openssl-1.1.1-relocation.patch
  patch -p1 -i ${srcdir}/openssl_master_date.patch
}

build() {
  # No support for out-of-source builds
  mkdir -p ${srcdir}/build-${CARCH}
  cp -a ${srcdir}/${_realname}-${_ver}/* ${srcdir}/build-${CARCH}

  # Use mingw cflags instead of hardcoded ones
  sed -i -e '/^"mingw"/ s/-fomit-frame-pointer -O3 -Wall/-O2 -pipe -Wall -Wp,-D_FORTIFY_SOURCE=2 -fexceptions --param=ssp-buffer-size=4/' ${srcdir}/build-${CARCH}/Configure

  _mingw=mingw
  if $(echo ${CARCH} | grep -q x86_64); then
    _mingw=mingw64
  fi
  cd "${srcdir}/build-${CARCH}"
  export MSYS2_ARG_CONV_EXCL="--prefix="

  CPPFLAGS+=" -DFD_SETSIZE=2048"

  ./Configure \
    --prefix=${MINGW_PREFIX} \
    --openssldir=ssl \
    --api=1.0.0 \
    ${_mingw} \
    shared \
    zlib-dynamic \
    enable-capieng \
    -D__MINGW_USE_VC2005_COMPAT \
    -DOPENSSLBIN=\"\\\"${MINGW_PREFIX}/bin\\\"\"

  cd "${srcdir}/build-${CARCH}"
  make -j1 ZLIB_INCLUDE=-I"${MINGW_PREFIX}"/include depend all
}

check() {
  cd "${srcdir}/build-${CARCH}"
#  make -j1 test
  make VERBOSE=1 test
}

package() {
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/bin
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/include/openssl
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/lib/engines-1_1
  mkdir    "${pkgdir}${MINGW_PREFIX}"/lib/pkgconfig
  mkdir -p "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}

  cd "${srcdir}/build-${CARCH}"
  make -j1 DESTDIR="${pkgdir}" MANDIR=${MINGW_PREFIX}/share/man MANSUFFIX=ssl install

  install -D -m644 LICENSE "${pkgdir}${MINGW_PREFIX}"/share/licenses/${_realname}/LICENSE

  chmod -R 777 "${pkgdir}${MINGW_PREFIX}"/bin
  chmod -R 777 "${pkgdir}${MINGW_PREFIX}"/lib
  chmod -R 777 "${pkgdir}${MINGW_PREFIX}"/lib/engines-1_1

  install -m644 ms/applink.c "${pkgdir}${MINGW_PREFIX}"/include/openssl/
  rm -rf "${pkgdir}${MINGW_PREFIX}"/bin/c_rehash
  rm -rf "${pkgdir}${MINGW_PREFIX}"/{ssl/misc,ssl/man}
}
